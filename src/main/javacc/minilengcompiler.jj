options
{
  static = true;
  ignore_case = true;
  common_token_action = true;
  debug_parser = false;
  java_unicode_escape = true;
}

PARSER_BEGIN(MiniLeng)
package com.minileng.generated;

import com.minileng.collections.FollowSets;
import com.minileng.collections.KindNameMapping;
import java.util.Set;
import java.util.ArrayList;
import java.util.List;
import java.io.IOException;
import com.minileng.codegen.CodeGenerator;
import com.minileng.collections.TokenFreq;
import com.minileng.exceptions.UnknownTokenException;
import com.minileng.semantic.Symbol;
import com.minileng.semantic.SymbolTable;
import org.apache.logging.log4j.LogManager;
import org.apache.logging.log4j.Logger;

public class MiniLeng {

  private static class Constexpr {
    public boolean isConst;
    public int value;
    public boolean isLValue;
    public Symbol sym;
    public boolean interpAsChar;
    public Constexpr(boolean isConst, int value, boolean isLValue) {
      this.isConst = isConst;
      this.value = value;
      this.isLValue = isLValue;
    }
    public Constexpr(boolean isConst, int value) {
      this.isConst = isConst;
      this.value = value;
      this.isLValue = false;
    }
    public Constexpr(boolean isConst) {
      this.isConst = isConst;
      this.value = -1;
    }
    public Constexpr() {
      this.isConst = false;
      this.value = -1;
      this.isLValue = false;
    }
  }

  private static class ParamsInfo {
    Symbol.ParameterType parType;
    Symbol.VariableType varType;
    List<Token> idTokens;
  }

  private static class IntWrapper {
    int value;
    public IntWrapper() {
      value = 3;
    }
    public IntWrapper(int value) {
      this.value = value;
    }
  }

  private static final Logger LOG = LogManager.getLogger();

  private static String buildMessage(ParseException e) {
    StringBuffer expected = new StringBuffer();
    int maxSize = 0;
    for (int i = 0; i < e.expectedTokenSequences.length; i++) {
      if (maxSize < e.expectedTokenSequences[i].length) {
        maxSize = e.expectedTokenSequences[i].length;
      }
      for (int j = 0; j < e.expectedTokenSequences[i].length; j++) {
        expected.append("<")
            .append(KindNameMapping.getInstance().name(e.expectedTokenSequences[i][j])).append(">")
            .append(", ");
      }
    }
    expected.delete(expected.length() - 2, expected.length());
    String retval = "Encontrado \"";
    Token tok = e.currentToken.next;
    for (int i = 0; i < maxSize; i++) {
      if (i != 0) {
        retval += " ";
      }
      if (tok.kind == 0) {
        retval += tokenImage[0];
        break;
      }
      retval += "<" + KindNameMapping.getInstance().name(tok.kind) + ">";
      retval += ":=";
      retval += ParseException.add_escapes(tok.image);
      retval += "\"";
      tok = tok.next;
    }
    retval += ". ";

    if (e.expectedTokenSequences.length == 0) {
      // Nothing to add here
    } else {
      if (e.expectedTokenSequences.length == 1) {
        retval += "Se esperaba: ";
      } else {
        retval += "Se esperaba uno de: ";
      }
      retval += expected.toString() + ".";
    }
    return retval;
  }

  private static void recoverParsingError(String nonterminal, ParseException e) {
    isThereAParsingError = true;
    LOG.info("ERROR SINTÁCTICO({}, {}): {}", e.currentToken.next.beginLine,
      e.currentToken.next.beginColumn, buildMessage(e));
    Set<String> followSet = FollowSets.getFollowSet(nonterminal);
    int pos;
    for (pos = 1; !followSet.contains(KindNameMapping.getInstance().name(getToken(pos).kind)) ; ++pos);
    for (int i = 0; i < pos - 1; ++i) getNextToken();
  }

  private static Symbol.VariableType toVariableType(int kind) {
    Symbol.VariableType type;
    switch(kind) {
      case tENTERO:
        type = Symbol.VariableType.INT;
        break;
      case tCARACTER:
        type = Symbol.VariableType.CHAR;
        break;
      case tBOOLEANO:
        type = Symbol.VariableType.BOOL;
        break;
      default:
        type = Symbol.VariableType.UNKNOWN;
        break;
    }
    return type;
  }

  private static boolean relation(Token relToken, int a, int b) {
    switch (relToken.kind ) {
      case tMAYOR:
        return a > b;
      case tMENOR:
        return a < b;
      case tIGUAL:
        return a == b;
      case tMAI:
        return a >= b;
      case tMEI:
        return a <= b;
      case tNI:
        return a != b;
      default:
        return false;
    }
  }

  private static boolean isValidVariableOrParameter(Token id) {
    Symbol sym = SymbolTable.getInstance().get(id.image);
    if (sym == null) {
      LOG.info("ERROR SEMÁNTICO({}, {}): Simbolo no declarado: \"{}\"", id.beginLine,
          id.beginColumn, id.image);
      return false;
    } else if (sym.getSymbolType() != Symbol.SymbolType.VARIABLE && sym.getSymbolType() != Symbol.SymbolType.PARAMETER) {
      LOG.info("ERROR SEMÁNTICO({}, {}): El simbolo no es una variable o un parámetro: \"{}\"",
          id.beginLine, id.beginColumn, id.image);
      return false;
    } else {
      return true;
    }
  }

  private static boolean isValidAction(Token id, int numArgs) {
    Symbol sym = SymbolTable.getInstance().get(id.image);
    if (sym == null) {
      LOG.info("ERROR SEMÁNTICO({}, {}): Simbolo no declarado: \"{}\"", id.beginLine,
          id.beginColumn, id.image);
      return false;
    } else if (sym.getSymbolType() != Symbol.SymbolType.ACTION) {
      LOG.info("ERROR SEMÁNTICO({}, {}): El simbolo no es una acción: \"{}\"",
          id.beginLine, id.beginColumn, id.image);
      return false;
    } else if (sym.getParameterList().size() != numArgs) {
      LOG.info("ERROR SEMÁNTICO({}, {}): Número de argumentos ({}) es distinto al número de"
        + " parámetros ({}) para la acción: \"{}\"", id.beginLine, id.beginColumn, numArgs,
        sym.getParameterList().size(), id.image);
      return false;
    } else {
      return true;
    }
  }

  private static void genExpDrf(Constexpr constexpr) {
    if (constexpr.isLValue) {
      gen.drf();
      if (constexpr.sym != null && constexpr.sym.isReference()) {
        gen.drf();
      }
    }
  }

  public static boolean wasThereAnError() {
    return isThereAParsingError || isThereASemanticError;
  }

  public static String getCodeAsString() {
    return gen.toString();
  }

  public static void writeCodeToFile(String filename) throws IOException {
    gen.writeToFile(filename);
  }

  private static CodeGenerator gen = new CodeGenerator();
  private static boolean isThereAParsingError = false;
  private static boolean isThereASemanticError = false;

}

PARSER_END(MiniLeng)

TOKEN_MGR_DECLS :
{
  private static TokenFreq tokenFreq = new TokenFreq();

  public static TokenFreq getTokenFreq() {
    return tokenFreq;
  }

  static void CommonTokenAction(Token t) {
    tokenFreq.addOrInc(t.kind);
  }
}

SKIP :
{
  " "
  | "\t"
  | "\n"
  | "\r"
  | "\r\n"
  | < tCOMENTARIOMULTI: "%%"(~[])*"%%" >
  | < tCOMENTARIO: "%"(~["\n","\r"])*("\n"|"\r"|"\r\n") >
}

// Data types
TOKEN :
{
  < tENTERO : "entero" >
  | < tBOOLEANO : "booleano" >
  | < tCARACTER : "caracter" >
}

// Arithmetic operators
TOKEN :
{
  < tMAS : "+" >
  | < tMENOS : "-" >
  | < tMUL : "*" >
  | < tDIVCHAR : "/" >
  | < tDIV : "div" >
  | < tMOD : "mod" >
}

// Asignment operators
TOKEN :
{
  < tOPAS : ":=" >
}

// Relational operators
TOKEN :
{
  < tMAYOR : ">" >
  | < tMENOR : "<" >
  | < tIGUAL : "=" >
  | < tMAI : ">=" >
  | < tMEI : "<=" >
  | < tNI : "<>" >
}

// Logical operators
TOKEN :
{
  < tAND : "and" >
  | < tOR : "or" >
  | < tNOT : "not" >
}

// Casting operators
TOKEN :
{
  < tENTACAR : "entacar" >
  | < tCARAENT : "caraent" >
}

// Conditional structures
TOKEN :
{
  < tSI : "si" >
  | < tENT : "ent" >
  | < tSI_NO : "si_no" >
  | < tFSI : "fsi" >
  | < tMQ : "mq" >
  | < tFMQ : "fmq" >
}

// IO
TOKEN :
{
  < tESCRIBIR : "escribir" >
  | < tLEER : "leer" >
}

// Procedure related tokens
TOKEN :
{
  < tVAL : "val" >
  | < tREF : "ref" >
  | < tACCION : "accion" >
  | < tPRINCIPIO : "principio" >
  | < tFIN : "fin" >
}

// Constants
TOKEN :
{
  < tTRUE : "true" >
  | < tFALSE : "false" >
  | < tKENTERO : (["0"-"9"])+ >
  | < tKCARACTER: "\""(~["\n","\r"])"\"" >
  | < tKCADENA: "\""(~["\n","\r","\""])(~["\n","\r","\""])+"\"" >
  | < tPUNTOCOMA : ";" >
  | < tCOMA : "," >
  | < tPAR_DCHA : ")" >
  | < tPAR_IZQ : "(" >
}

// Program
TOKEN :
{
  < tPROGRAMA : "programa" >
  | < tID : (["a"-"z"]) | (["a"-"z","_"](["a"-"z","0"-"9","_"])*(["a"-"z","0"-"9"])+) >
}

// Error
TOKEN :
{
  < tERROR : ~[] >
  {
    if (true) {
      throw new UnknownTokenException(
          "ERROR LÉXICO (" + matchedToken.beginLine + ", " + matchedToken.beginColumn
              + "): símbolo no reconocido: " + image);
    }
  }
}

void programa() :
{
  Token p;
  String mainLabel;
  IntWrapper freeAddress = new IntWrapper();
}

{
  try {
    {
      isThereAParsingError = false;
      isThereASemanticError = false;
      gen.reinit();
      mainLabel = gen.newLabel();
      gen.enp(mainLabel);
    }
    <tPROGRAMA> { SymbolTable.getInstance().openScope(); }
    p = <tID> { SymbolTable.getInstance().putProgram(p.image); }
    <tPUNTOCOMA> declaracion_variables(freeAddress) declaracion_acciones()
    {gen.insertLabel(mainLabel);} bloque_sentencias(freeAddress) <EOF>
    {
      SymbolTable.getInstance().closeScope();
      gen.lvp();
    }
  } catch (UnknownTokenException e) {
    LOG.info(e.getMessage());
  } catch (ParseException e) {
    recoverParsingError("programa", e);
  }
}

void declaracion_variables(IntWrapper addr) :
{
}

{
  try {
    declaracion(addr) <tPUNTOCOMA> declaracion_variables(addr) | {}
  } catch (ParseException e) {
    recoverParsingError("declaracion_variables", e);
  }
}

void declaracion(IntWrapper addr) :
{
  Symbol.VariableType variableType;
  List<Token> tokens;
}

{
  try {
    variableType = tipo_variables() tokens = identificadores()
    {
      for (Token id : tokens) {
          if(null == SymbolTable.getInstance().putVariable(id.image, variableType, addr.value++)) {
            LOG.info("ERROR SEMÁNTICO({}, {}): Simbolo ya declarado previamente: \"{}\"",
                id.beginLine, id.beginColumn, id.image);
          }
      }
    }
  } catch (ParseException e) {
    recoverParsingError("declaracion", e);
  }
}

Symbol.VariableType tipo_variables() :
{
  Token t = null;
}

{
  try {
    t = <tENTERO> | t = <tCARACTER> | t = <tBOOLEANO>
  } catch (ParseException e) {
    recoverParsingError("tipo_variables", e);
  }
  { return t == null ? null : toVariableType(t.kind); }
}

List<Token> identificadores() :
{
  List<Token> tokens = new ArrayList<Token>();
  List<Token> tokensRep;
  Token id;
}

{
  try {
    id = <tID> {tokens.add(id);}
    tokensRep = repeticion_identificadores() {tokens.addAll(tokensRep);}
  } catch (ParseException e) {
    recoverParsingError("identificadores", e);
    tokens = new ArrayList<Token>();
  }
  {return tokens;}
}

List<Token> repeticion_identificadores() :
{
  List<Token> tokens = new ArrayList<Token>();
  List<Token> tokensRep;
  Token id;
}

{
  try {
    <tCOMA> id = <tID> {tokens.add(id);}
    tokensRep = repeticion_identificadores() {tokens.addAll(tokensRep);}
    | {}
  } catch (ParseException e) {
    recoverParsingError("repeticion_identificadores", e);
    tokens = new ArrayList<Token>();
  }
  {return tokens;}
}

void declaracion_acciones() :
{
}

{
  try {
    declaracion_accion() declaracion_acciones() | {}
  } catch (ParseException e) {
    recoverParsingError("declaracion_acciones", e);
  }
}

void declaracion_accion() :
{
  IntWrapper addr = new IntWrapper();
}

{
  try {
    cabecera_accion(addr) <tPUNTOCOMA> declaracion_variables(addr)
    declaracion_acciones() bloque_sentencias(addr)
    {
      gen.csf();
      SymbolTable.getInstance().closeScope();
    }
  } catch (ParseException e) {
    recoverParsingError("declaracion_accion", e);
  }
}

void cabecera_accion(IntWrapper addr) :
{
  List<ParamsInfo> params;
  Token actionId = null;
  int currentScope = SymbolTable.getInstance().getCurrentScope();
}

{
  try {
    <tACCION> actionId = <tID> params = parametros_formales()
    {
      List<Symbol> paramSyms = new ArrayList<Symbol>();
      for (ParamsInfo paramsInfo : params) {
        for (Token idToken : paramsInfo.idTokens) {
          // The scope and the address doesn't really matter here
          Symbol param = Symbol.buildParameter(idToken.image, paramsInfo.varType,
              paramsInfo.parType, currentScope + 1, addr.value++);
          paramSyms.add(param);
        }
      }
      if(null == SymbolTable.getInstance().putAction(actionId.image, paramSyms,
          gen.getInstrCount())) {
        LOG.info("ERROR SEMÁNTICO({}, {}): Simbolo ya declarado previamente: \"{}\"",
            actionId.beginLine, actionId.beginColumn, actionId.image);
      }
      SymbolTable.getInstance().openScope();
      {
        int i = 0;
        for (ParamsInfo paramsInfo : params) {
          for (Token idToken : paramsInfo.idTokens) {
            if (SymbolTable.getInstance().putSymbol(paramSyms.get(i++)) == null) {
              LOG.info("ERROR SEMÁNTICO({}, {}): Simbolo ya declarado previamente: \"{}\"",
                  idToken.beginLine, idToken.beginColumn, idToken.image);
            }
          }
        }
      }
      gen.comment("Accion: " + actionId.image);
      for (int i = paramSyms.size() - 1; i >= 0; --i) {
        Symbol s = paramSyms.get(i);
        gen.srf(0, s.getAddress());
        gen.asgi();
      }
    }
  } catch (ParseException e) {
    recoverParsingError("cabecera_accion", e);
  }
}

List<ParamsInfo> parametros_formales() :
{
  List<ParamsInfo> ids;
}

{
  try {
    <tPAR_IZQ> ids = lista_parametros() <tPAR_DCHA>
    | {} {ids = new ArrayList<ParamsInfo>();}
  } catch (ParseException e) {
    recoverParsingError("parametros_formales", e);
    ids = new ArrayList<ParamsInfo>();
  }
  {return ids;}
}

List<ParamsInfo> lista_parametros() :
{
  List<ParamsInfo> paramsInfoList = new ArrayList<ParamsInfo>();
  List<ParamsInfo> paramsInfoRepList;
  ParamsInfo paramsInfo;
}

{
  try {
    paramsInfo = parametro() {paramsInfoList.add(paramsInfo);}
    paramsInfoRepList = repeticion_parametro()
    {paramsInfoList.addAll(paramsInfoRepList);}
    | {} {paramsInfoList = new ArrayList<ParamsInfo>();}
  } catch (ParseException e) {
    recoverParsingError("lista_parametros", e);
    paramsInfoList = new ArrayList<ParamsInfo>();
  }
  {return paramsInfoList;}
}

ParamsInfo parametro() :
{
  ParamsInfo paramsInfo = new ParamsInfo();
}

{
  try {
    paramsInfo.parType = clase_parametros()
    paramsInfo.varType = tipo_variables()
    paramsInfo.idTokens = identificadores()
  } catch (ParseException e) {
    recoverParsingError("parametro", e);
    paramsInfo = new ParamsInfo();
  }
  {return paramsInfo;}
}

List<ParamsInfo> repeticion_parametro() :
{
  List<ParamsInfo> paramsInfoList = new ArrayList<ParamsInfo>();
  List<ParamsInfo> paramsInfoRepList;
  ParamsInfo paramsInfo;
}

{
  try {
    <tPUNTOCOMA> paramsInfo = parametro() {paramsInfoList.add(paramsInfo);}
    paramsInfoRepList = repeticion_parametro() {paramsInfoList.addAll(paramsInfoRepList);}
    | {} {paramsInfoList = new ArrayList<ParamsInfo>();}
  } catch (ParseException e) {
    recoverParsingError("repeticion_parametro", e);
    paramsInfoList = new ArrayList<ParamsInfo>();
  }
  {return paramsInfoList;}
}

Symbol.ParameterType clase_parametros() :
{
  Symbol.ParameterType parameterType = null;
}

{
  try {
    <tVAL> {parameterType = Symbol.ParameterType.VAL;}
    | <tREF> {parameterType = Symbol.ParameterType.REF;}
  } catch (ParseException e) {
    recoverParsingError("clase_parametros", e);
  }
  {return parameterType;}
}

void bloque_sentencias(IntWrapper freeAddress) :
{
}

{
  try {
    <tPRINCIPIO> lista_sentencias(freeAddress) <tFIN>
  } catch (ParseException e) {
    recoverParsingError("bloque_sentencias", e);
  }
}

void lista_sentencias(IntWrapper freeAddress) :
{
}

{
  try {
    sentencia(freeAddress) repeticion_lista_sentencias(freeAddress)
  } catch (ParseException e) {
    recoverParsingError("lista_sentencias", e);
  }
}

void repeticion_lista_sentencias(IntWrapper freeAddress) :
{
}

{
  try {
    sentencia(freeAddress) repeticion_lista_sentencias(freeAddress) | {}
  } catch (ParseException e) {
    recoverParsingError("repeticion_lista_sentencias", e);
  }
}

void sentencia(IntWrapper freeAddress) :
{
}

{
  try {
    leer() <tPUNTOCOMA> | escribir() <tPUNTOCOMA>
    | asignacion_invocacion(freeAddress) | seleccion(freeAddress)
    | mientras_que(freeAddress) | <tPUNTOCOMA>
  } catch (ParseException e) {
    recoverParsingError("sentencia", e);
  }
}

void leer() :
{
  List<Token> idTokens;
}

{
  try {
    <tLEER> <tPAR_IZQ> idTokens = identificadores()
    {
      for (Token id : idTokens) {
        Symbol symId = SymbolTable.getInstance().get(id.image);
        if (isValidVariableOrParameter(id)) {
          gen.srf(0, symId.getAddress());
          if (symId.isReference()) {
            gen.drf();
          }
          gen.rd(symId.getVariableType() == Symbol.VariableType.INT);
        }
      }
    }
    <tPAR_DCHA>
  } catch (ParseException e) {
    recoverParsingError("leer", e);
  }
}

void escribir() :
{
}

{
  try {
    <tESCRIBIR> <tPAR_IZQ> lista_argumentos_escribir() <tPAR_DCHA>
  } catch (ParseException e) {
    recoverParsingError("escribir", e);
  }
}

void asignacion_invocacion(IntWrapper freeAddress) :
{
  Token id;
}

{
  try {
    id = <tID> asignacion_invocacion_(id, freeAddress)
  } catch (ParseException e) {
    recoverParsingError("asignacion_invocacion", e);
  }
}

void asignacion_invocacion_(Token id, IntWrapper freeAddress) :
{
  int numArgs;
  Symbol idSym = SymbolTable.getInstance().get(id.image);
}

{
  try {
    // TODO: put the calls to asignacion() and invocacion_accion() after checking the id symbol not before
    asignacion()
    {
      if (isValidVariableOrParameter(id)) {
        int frame = SymbolTable.getInstance().getCurrentScope() - idSym.getScope();
        gen.srf(frame, idSym.getAddress());
        if (idSym.isReference()) {
          gen.drf();
        }
        gen.asgi();
      }
    }
    | numArgs = invocacion_accion(freeAddress, idSym)
    {
      if (isValidAction(id, numArgs)) {
      }
    }
  } catch (ParseException e) {
    recoverParsingError("asignacion_invocacion_", e);
  }
}

void asignacion() :
{
  Constexpr constexpr;
}

{
  try {
    <tOPAS> constexpr = expresion_bool()
    {
      genExpDrf(constexpr);
    }
    <tPUNTOCOMA>
  } catch (ParseException e) {
    recoverParsingError("asignacion", e);
  }
}

int invocacion_accion(IntWrapper freeAddress, Symbol actionSym) :
{
  int numArgs;
}

{
  try {
    numArgs = argumentos_accion(freeAddress, actionSym) <tPUNTOCOMA>
    {
      if (actionSym != null && actionSym.isAction()) {
        gen.osf(freeAddress.value,
            SymbolTable.getInstance().getCurrentScope() - actionSym.getScope(),
            actionSym.getAddress());
      }
    }
  } catch (ParseException e) {
    recoverParsingError("invocacion_accion", e);
    numArgs = 0;
  }
  {return numArgs;}
}

void mientras_que(IntWrapper freeAddress) :
{
  String startLoopLabel;
  String endLoopLabel;
  Constexpr constexpr;
}

{
  try {
    <tMQ> {startLoopLabel = gen.newLabel(); gen.insertLabel(startLoopLabel);}
    constexpr = expresion_bool()
    {
      genExpDrf(constexpr);
      endLoopLabel = gen.newLabel();
      gen.jmf(endLoopLabel);
    }
    lista_sentencias(freeAddress) {gen.jmp(startLoopLabel);}
    <tFMQ> {gen.insertLabel(endLoopLabel);}
  } catch (ParseException e) {
    recoverParsingError("mientras_que", e);
  }
}

void seleccion(IntWrapper freeAddress) :
{
}

{
  try {
    <tSI> seleccion_(freeAddress)
  } catch (ParseException e) {
    recoverParsingError("seleccion", e);
  }
}

int argumentos_accion(IntWrapper freeAddress, Symbol actionSym) :
{
  int numArgs = 0;
}

{
  try {
    <tPAR_IZQ> numArgs = lista_argumentos_vacia(freeAddress, actionSym)
    <tPAR_DCHA> | {}
  } catch (ParseException e) {
    recoverParsingError("argumentos_accion", e);
    numArgs = 0;
  }
  {return numArgs;}
}

void argumento_escribir() :
{
  Token tCadena;
  Constexpr constexpr;
}

{
  try {
    tCadena = <tKCADENA>
    {
//      for (int i = tCadena.image.length() - 1; i >= 0; --i) {
//        gen.stc(tCadena.image.charAt(i));
//        gen.wrt(false);
//      }
      for (int i = 1; i < tCadena.image.length() - 1; ++i) {
        gen.stc(tCadena.image.charAt(i));
        gen.wrt(false);
      }
    }
    | constexpr = expresion_bool()
    {genExpDrf(constexpr); gen.wrt(!constexpr.interpAsChar);}
  } catch (ParseException e) {
    recoverParsingError("argumento_escribir", e);
  }
}

int lista_argumentos_vacia(IntWrapper freeAddress, Symbol actionSym) :
{
  Constexpr constexpr;
  IntWrapper argPos = new IntWrapper(0);
}

{
  try {
    constexpr = expresion_bool()
    {
      if (actionSym != null && actionSym.isAction()) {
        List<Symbol> params = actionSym.getParameterList();
        if (argPos.value < params.size()) {
          Symbol param = params.get(argPos.value);
          if (!constexpr.isLValue) {
            if (param.isReference()) {
              gen.srf(0, freeAddress.value);
              gen.asgi();
              gen.srf(0, freeAddress.value++);
            }
          } else {
            if (constexpr.sym.isReference()) {
              gen.drf();
            }
            if (!param.isReference()) {
              gen.drf();
            }
          }
        }
      }
    }
    repeticion_argumentos(freeAddress, actionSym, argPos) | {}
  } catch (ParseException e) {
    recoverParsingError("lista_argumentos_vacia", e);
    return 0;
  }
  {return argPos.value + 1;}
}

void lista_argumentos_escribir() :
{
  Token tCadena;
  Constexpr constexpr;
}

{
  try {
    tCadena = <tKCADENA>
    {
//      for (int i = tCadena.image.length() - 1; i >= 0; --i) {
//        gen.stc(tCadena.image.charAt(i));
//        gen.wrt(false);
//      }
      for (int i = 1; i < tCadena.image.length() - 1; ++i) {
        gen.stc(tCadena.image.charAt(i));
        gen.wrt(false);
      }
    }
    repeticion_argumentos_escribir()
    | constexpr = expresion_bool() {genExpDrf(constexpr); gen.wrt(!constexpr.interpAsChar);}
    repeticion_argumentos_escribir()
  } catch (ParseException e) {
    recoverParsingError("lista_argumentos_escribir", e);
  }
}

void repeticion_argumentos(IntWrapper freeAddress, Symbol actionSym,
    IntWrapper argPos) :
{
  Constexpr constexpr;
}

{
  try {
    <tCOMA> constexpr = expresion_bool()
    {
      argPos.value++;
      if (actionSym != null && actionSym.isAction()) {
        List<Symbol> params = actionSym.getParameterList();
        if (argPos.value < params.size()) {
          Symbol param = params.get(argPos.value);
          if (!constexpr.isLValue) {
            if (param.isReference()) {
              gen.srf(0, freeAddress.value);
              gen.asgi();
              gen.srf(0, freeAddress.value++);
            }
          } else {
            if (constexpr.sym.isReference()) {
              gen.drf();
            }
            if (!param.isReference()) {
              gen.drf();
            }
          }
        }
      }
    }
    repeticion_argumentos(freeAddress, actionSym, argPos) | {}
  } catch (ParseException e) {
    recoverParsingError("repeticion_argumentos", e);
  }
}

void repeticion_argumentos_escribir() :
{
}

{
  try {
    <tCOMA> argumento_escribir() repeticion_argumentos_escribir() | {}
  } catch (ParseException e) {
    recoverParsingError("repeticion_argumentos_escribir", e);
  }
}

Token operador_relacional() :
{
  Token t = null;
}

{
  try {
    t = <tMAYOR> | t = <tMENOR> | t = <tIGUAL> | t = <tMAI> | t = <tMEI>
    | t = <tNI>
  } catch (ParseException e) {
    recoverParsingError("operador_relacional", e);
  }
  {return t;}
}

void caraent() :
{
}

{
  try {
    <tCARAENT> caraent_()
  } catch (ParseException e) {
    recoverParsingError("caraent", e);
  }
}

Constexpr entacar() :
{
   Constexpr constexpr = new Constexpr();
   Token t;
}

{
  try {
    t = <tENTACAR> <tPAR_IZQ> constexpr = expresion_bool()
    {
      genExpDrf(constexpr);
      constexpr.interpAsChar = true;
      if (constexpr.isConst && (constexpr.value < 0 || constexpr.value > 255)) {
        LOG.info("ERROR SEMÁNTICO({}, {}): El argumento de 'entacar' ha de tener un valor"
            + "comprendido entre 0 y 255, ambos incluidos", t.beginLine, t.beginColumn);
      }
    }
    <tPAR_DCHA>
  } catch (ParseException e) {
    recoverParsingError("entacar", e);
  }
  {return constexpr;}
}

Constexpr expresion_bool() :
{
  Constexpr constexprTermBool = new Constexpr();
  Constexpr constexprExpresionBool = new Constexpr();
}

{
  try {
    constexprTermBool = term_bool()
    constexprExpresionBool = expresion_bool_(constexprTermBool)
  } catch (ParseException e) {
    recoverParsingError("expresion_bool", e);
  }
  {return constexprExpresionBool;}
}

Constexpr term_bool() :
{
  Constexpr factor = new Constexpr();
  Constexpr term = new Constexpr();
}

{
  try {
    factor = factor_bool_unario() term = term_bool_(factor)
  } catch (ParseException e) {
    recoverParsingError("term_bool", e);
  }
  {return term;}
}

Constexpr factor_bool_unario() :
{
  Constexpr constexpr = new Constexpr();
}

{
  try {
    <tNOT> constexpr = factor_bool()
    {
      constexpr.value = constexpr.value != 0 ? 0 : 1;
      genExpDrf(constexpr);
      constexpr.isLValue = false;
      gen.ngb();
    }
    | constexpr = factor_bool()
  } catch (ParseException e) {
    recoverParsingError("factor_bool_unario", e);
  }
  {return constexpr;}
}

Constexpr factor_bool() :
{
  Constexpr constexpr = new Constexpr();
}

{
  try {
    <tFALSE>
    {
      constexpr.isConst = true;
      constexpr.value = 0;
      gen.stc(0);
    }
    | <tTRUE>
    {
      constexpr.isConst = true;
      constexpr.value = 1;
      gen.stc(1);
    }
    | constexpr = relacion()
  } catch (ParseException e) {
    recoverParsingError("factor_bool", e);
  }
  {return constexpr;}
}

Constexpr relacion() :
{
  Constexpr expr = new Constexpr();
  Constexpr rel = new Constexpr();
}

{
  try {
    expr = expresion_arit() rel = relacion_(expr)
  } catch (ParseException e) {
    recoverParsingError("relacion", e);
  }
  {return rel;}
}

Constexpr expresion_arit() :
{
  Constexpr constexpr = new Constexpr();
  Constexpr term = new Constexpr();
  Constexpr exp = new Constexpr();
}

{
  try {
    term = term_arit() exp = expresion_arit_(term)
  } catch (ParseException e) {
    recoverParsingError("expresion_arit", e);
  }
  {return exp;}
}

Constexpr term_arit() :
{
  Constexpr constexpr = new Constexpr();
  Constexpr factor = new Constexpr();
  Constexpr term = new Constexpr();
}

{
  try {
    factor = factor_arit_unario() term = term_arit_(factor)
  } catch (ParseException e) {
    recoverParsingError("term_arit", e);
  }
  {return term;}
}

Constexpr factor_arit_unario() :
{
  Constexpr constexpr = new Constexpr();
}

{
  try {
    <tMAS> constexpr = factor_arit() {constexpr.interpAsChar = false;}
    | <tMENOS> constexpr = factor_arit()
    {
      constexpr.value = -1 * constexpr.value;
      constexpr.isLValue = false;
      constexpr.interpAsChar = false;
      genExpDrf(constexpr);
      gen.ngi();
    }
    | constexpr = factor_arit()
  } catch (ParseException e) {
    recoverParsingError("factor_arit_unario", e);
  }
  {return constexpr;}
}

Constexpr factor_arit() :
{
  Token id;
  Constexpr constexpr = new Constexpr();
}

{
  try {
    id = <tKENTERO>
    {
      constexpr.isConst = true;
      constexpr.value = Integer.valueOf(id.image);
      gen.stc(constexpr.value);
    }
    | id = <tID>
    {
      Symbol symId = SymbolTable.getInstance().get(id.image);
      if (isValidVariableOrParameter(id)) {
        constexpr.isLValue = true;
        constexpr.sym = symId;
        constexpr.interpAsChar = symId.getVariableType() == Symbol.VariableType.CHAR;
        gen.srf(SymbolTable.getInstance().getCurrentScope() - symId.getScope(),
            symId.getAddress());
      }
    }
    | id = <tKCARACTER>
    {
      constexpr.isConst = true;
      constexpr.value = id.image.charAt(1);
      constexpr.interpAsChar = true;
      gen.stc(constexpr.value);
    }
    | <tPAR_IZQ> constexpr = expresion_bool() {genExpDrf(constexpr);} <tPAR_DCHA>
    | constexpr = entacar()
    | <tCARAENT> constexpr = factor_arit_()
  } catch (ParseException e) {
    recoverParsingError("factor_arit", e);
  }
  {return constexpr;}
}

Constexpr expresion_bool_(Constexpr inherit) :
{
  Constexpr constexpr = new Constexpr();
  Constexpr term = new Constexpr();
  Constexpr exp = new Constexpr();
}

{
  try {
    <tOR> {genExpDrf(inherit);}
    term = term_bool() {genExpDrf(term); gen.or();}
    exp = expresion_bool_(term)
    {
      constexpr.isConst = inherit.isConst && exp.isConst;
      constexpr.value = (inherit.value != 0) || (exp.value != 0) ? 1 : 0;
    }
    | {} {constexpr = inherit;}
  } catch (ParseException e) {
    recoverParsingError("expresion_bool_", e);
  }
  {return constexpr;}
}

Constexpr term_bool_(Constexpr inherit) :
{
  Constexpr constexpr = new Constexpr();
  Constexpr factor = new Constexpr();
  Constexpr term = new Constexpr();
}

{
  try {
    <tAND> {genExpDrf(inherit);}
    factor = factor_bool_unario()
    {genExpDrf(factor); gen.and();}
    term = term_bool_(factor)
    {
      constexpr.isConst = inherit.isConst && term.isConst;
      constexpr.value = (inherit.value != 0) && (term.value != 0) ? 1 : 0;
    }
    | {} {constexpr = inherit;}
  } catch (ParseException e) {
    recoverParsingError("term_bool_", e);
  }
  {return constexpr;}
}

Constexpr expresion_arit_(Constexpr inherit) :
{
  Constexpr constexpr = new Constexpr();
  Constexpr term = new Constexpr();
  Constexpr exp = new Constexpr();
}

{
  try {
    <tMAS> {genExpDrf(inherit);}
    term = term_arit() {genExpDrf(term); gen.plus();}
    exp = expresion_arit_(term)
    {
      constexpr.isConst = inherit.isConst && term.isConst && exp.isConst;
      constexpr.value = inherit.value + exp.value;
    }
    | <tMENOS> {genExpDrf(inherit);}
    term = term_arit()
    {genExpDrf(term); gen.sbt();}
    exp = expresion_arit_(term)
    {
      constexpr.isConst = inherit.isConst && term.isConst && exp.isConst;
      constexpr.value = inherit.value - exp.value;
    }
    | {} {constexpr = inherit;}
  } catch (ParseException e) {
    recoverParsingError("expresion_arit_", e);
  }
  {return constexpr;}
}

Constexpr term_arit_(Constexpr inherit) :
{
    Constexpr constexpr = new Constexpr();
    Constexpr factor = new Constexpr();
    Constexpr term = new Constexpr();
    Token t;
}

{
  try {
    <tMUL> {genExpDrf(inherit);}
    factor = factor_arit_unario()
    {genExpDrf(factor); gen.tms();}
    term = term_arit_(factor)
    {
      if (inherit.isConst && factor.isConst) {
        constexpr.isConst = true;
        constexpr.value = inherit.value * term.value;
      } else if ((inherit.isConst && inherit.value == 0)
            || (factor.isConst && factor.value == 0)) {
        constexpr.isConst = true;
        constexpr.value = 0;
      }
    }
    | t = <tDIV> {genExpDrf(inherit);}
    factor = factor_arit_unario()
    {genExpDrf(factor); gen.div();}
    term = term_arit_(factor)
    {
      if (term.isConst) {
        if (term.value == 0) {
          LOG.info("ERROR SEMÁNTICO({}, {}): Division por cero", t.beginLine, t.beginColumn);
        } else {
          constexpr.isConst = inherit.isConst && term.isConst && factor.isConst;
          constexpr.value = inherit.value / term.value;
        }
      }
    }
    | t = <tDIVCHAR> {genExpDrf(inherit);}
    factor = factor_arit_unario()
    {genExpDrf(factor); gen.div();}
    term = term_arit_(factor)
    {
      if (term.isConst) {
        if (term.value == 0) {
          LOG.info("ERROR SEMÁNTICO({}, {}): Division por cero", t.beginLine, t.beginColumn);
        } else {
          constexpr.isConst = inherit.isConst && term.isConst && factor.isConst;
          constexpr.value = inherit.value / term.value;
        }
      }
    }
    | t = <tMOD> {genExpDrf(inherit);}
    factor = factor_arit_unario()
    {genExpDrf(factor); gen.mod();}
    term = term_arit_(factor)
    {
      if (term.isConst && term.value == 0) {
        LOG.info("ERROR SEMÁNTICO({}, {}): Modulo cero", t.beginLine, t.beginColumn);
      } else {
        constexpr.isConst = inherit.isConst && term.isConst && factor.isConst;
        constexpr.value = inherit.value % term.value;
      }
    }
    | {} {constexpr = inherit;}
  } catch (ParseException e) {
    recoverParsingError("term_arit_", e);
  }
  {return constexpr;}
}

void seleccion_(IntWrapper freeAddress) :
{
  Constexpr constexpr;
}
{
  try {
    constexpr = expresion_bool()
    {
      genExpDrf(constexpr);
      String labelFalse = gen.newLabel();
      gen.jmf(labelFalse);
    }
    seleccion__(labelFalse, freeAddress)
  } catch (ParseException e) {
    recoverParsingError("seleccion_", e);
  }
}

Constexpr caraent_() :
{
  Constexpr constexpr = new Constexpr();
}

{
  try {
    <tPAR_IZQ> constexpr = caraent__()
  } catch (ParseException e) {
    recoverParsingError("caraent_", e);
  }
  {return constexpr;}
}

Constexpr relacion_(Constexpr inherit) :
{
  Constexpr constexpr = new Constexpr();
  Constexpr exp = new Constexpr();
  Token op;
}

{
  try {
    op = operador_relacional() {genExpDrf(inherit);}
    exp = expresion_arit()
    {
      genExpDrf(exp);
      constexpr.value = relation(op, inherit.value, exp.value) ? 1 : 0;
      constexpr.isConst = inherit.isConst && exp.isConst;
      if (!constexpr.isConst) {
        switch (op.kind ) {
          case tMAYOR:
            gen.gt();
            break;
          case tMENOR:
            gen.lt();
            break;
          case tIGUAL:
            gen.eq();
            break;
          case tMAI:
            gen.gte();
            break;
          case tMEI:
            gen.lte();
            break;
          case tNI:
            gen.neq();
            break;
          default:
            gen.nop();
        }
      }
    }
    | {} {constexpr = inherit;}
  } catch (ParseException e) {
    recoverParsingError("relacion_", e);
  }
  {return constexpr;}
}

Constexpr factor_arit_() :
{
  Constexpr constexpr = new Constexpr();
}

{
  try {
    <tPAR_IZQ> constexpr = factor_arit__()
  } catch (ParseException e) {
    recoverParsingError("factor_arit_", e);
  }
  {return constexpr;}
}

void seleccion__(String labelFalse, IntWrapper freeAddress) :
{
}

{
  try {
    <tENT> seleccion___(labelFalse, freeAddress)
  } catch (ParseException e) {
    recoverParsingError("seleccion__", e);
  }
}

Constexpr caraent__() :
{
  Token id;
  Constexpr constexpr = new Constexpr();
}

{
  try {
    id = <tKCARACTER> <tPAR_DCHA>
    {
      constexpr.isConst = true;
      constexpr.value = id.image.charAt(1);
      gen.stc(id.image.charAt(1));
    }
    | constexpr = entacar() <tPAR_DCHA>
    | id = <tID>
    {
      Symbol symId = SymbolTable.getInstance().get(id.image);
      if (isValidVariableOrParameter(id)) {
        gen.srf(SymbolTable.getInstance().getCurrentScope() - symId.getScope(),
            symId.getAddress());
        gen.drf();
        if (symId.isReference()) {
          gen.drf();
        }
      }
    }
    <tPAR_DCHA>
  } catch (ParseException e) {
    recoverParsingError("caraent__", e);
  }
  {return constexpr;}
}

Constexpr factor_arit__() :
{
  Token id;
  Constexpr constexpr = new Constexpr();
}

{
  try {
    id = <tKCARACTER> <tPAR_DCHA>
    {
      constexpr.isConst = true;
      constexpr.value = id.image.charAt(1);
      gen.stc(id.image.charAt(1));
    }
    | constexpr = entacar() <tPAR_DCHA>
    | id = <tID>
    {
      Symbol sym = SymbolTable.getInstance().get(id.image);
      if (isValidVariableOrParameter(id)) {
        gen.srf(SymbolTable.getInstance().getCurrentScope() - sym.getScope(),
            sym.getAddress());
        gen.drf();
        if (sym.isReference()) {
          gen.drf();
        }
      }
    }
    <tPAR_DCHA>
  } catch (ParseException e) {
    recoverParsingError("factor_arit__", e);
  }
  {return constexpr;}
}

void seleccion___(String labelFalse, IntWrapper freeAddress) :
{
}

{
  try {
    lista_sentencias(freeAddress) seleccion____(labelFalse, freeAddress)
  } catch (ParseException e) {
    recoverParsingError("seleccion___", e);
  }
}

void seleccion____(String labelFalse, IntWrapper freeAddress) :
{
}

{
  try {
    <tFSI> {gen.insertLabel(labelFalse);}
    | {String l = gen.newLabel(); gen.jmp(l);}
    <tSI_NO> {gen.insertLabel(labelFalse);} lista_sentencias(freeAddress)
    <tFSI> {gen.insertLabel(l);}
  } catch (ParseException e) {
    recoverParsingError("seleccion____", e);
  }
}
